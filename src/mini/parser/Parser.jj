PARSER_BEGIN(MiniParser)
package mini.parser;
import java.util.List;
import java.util.ArrayList;
import mini.parser.ast.*;
import mini.parser.ast.expr.*;
public class MiniParser {
}
PARSER_END(MiniParser)

SKIP: { " " | "\t" | "\r" | "\n" }
TOKEN: {
    <NUM : (["0"-"9"])+ > |
    <FN : "fn" > |
    <ASSIGN : ":=" > |
    <LET : "let" > |
    <RETURN : "return" > |
    <IDENT : (["a"-"z", "A"-"Z"])+ >
}

List<FunDecl> Prog():
  { List<FunDecl> ds = new ArrayList<FunDecl>(); FunDecl d; }
  {
    (
      d = FunDecl() { ds.add(d); }
    )*
    { return ds; }
  }

FunDecl FunDecl():
  {  Token t; List<String> ps = new ArrayList<String>(); List<Statement> bd; }
  {
    <FN>
    t = <IDENT>
    "(" (ps = Params())? ")"
    "{" bd = Body() "}"
    { return new FunDecl(t.image, ps, bd); }
  }

List<String> Params():
  { Token p; List<String> ps = new ArrayList<String>(); }
  {
    p = <IDENT> { ps.add(p.image); }
    (
      "," p = <IDENT> { ps.add(p.image); }
    )*
    { return ps; }
  }

List<Statement> Body():
  { Statement st; List<Statement> sts = new ArrayList<Statement>(); }
  {
    (
      st = Statement() ";" { sts.add(st); }
    )*
    { return sts; }
  }

Statement Statement():
  { Token t; IExpr e; }
  {
    (
      t = <IDENT>
      <ASSIGN>
      e = Expr()
      { return new AssignStatement(t.image, e); }
    ) | (
      <LET>
      t = <IDENT>
      <ASSIGN>
      e = Expr()
      { return new LetStatement(t.image, e); }
    ) | (
       <RETURN>
       e = Expr()
      { return new ReturnStatement(e); }
    )
  }

IExpr Expr():
  { IExpr a, b; Token n; }
  {
    a = Term()
    (
      "+" b = Expr() { a = new BinaryExpr(BinaryExprType.ADD, a, b); }
    | "-" b = Expr() { a = new BinaryExpr(BinaryExprType.SUB, a, b); }
    )?
    { return a; }
  }

IExpr Term():
  { IExpr a, b; }
  {
    a = Unary()
    (
      "*" b = Term() { a = new BinaryExpr(BinaryExprType.MUL, a, b); }
    )?
    { return a; }
  }

IExpr Unary():
  { IExpr a; }
  {
    "-" a = Elem() { return new UnaryExpr(UnaryExprType.NEG, a); }
  | a = Elem()     { return a; }
  }

IExpr Elem():
  { Token t; IExpr a; }
  {
    t = <IDENT> { return new IdentExpr(t.image); }
  | t = <NUM>   { return new NumExpr(Integer.parseInt(t.image)); }
  | "(" a = Expr() ")" { return a; }
  }





